plugins {
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.9.0'
    id "com.github.ben-manes.versions" version "0.8"
}

ext {
    libraries = [:]
    versions = [:]
}

versions += [
        spock        : '1.0-groovy-2.3',
        jenkinsJobDsl: '1.28',
]

libraries += [
        jenkinsJobDsl: "org.jenkins-ci.plugins:job-dsl-core:${versions.jenkinsJobDsl}",
        logging      : 'ch.qos.logback:logback-classic:1.1.2',
        spock        : "org.spockframework:spock-core:${versions.spock}",
        spockDeps    : [
                'org.objenesis:objenesis:2.1',
                'cglib:cglib-nodep:3.1',
        ],
]

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile libraries.jenkinsJobDsl

    testCompile(libraries.spock) {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testCompile libraries.spockDeps
}

pluginBundle {
    website = 'https://github.com/pvdissel/gradle-jenkins-jobdsl'
    vcsUrl = 'https://github.com/pvdissel/gradle-jenkins-jobdsl'
    description = 'A Gradle plugin that let\'s you build and generate your Jenkins JobDSL scripts'
    tags = ['jenkins', 'jobdsl']

    plugins {
        jenkinsJobDsl {
            id = 'nl.ikoodi.jenkins.jobdsl'
            displayName = 'Gradle Jenkins JobDSL Plugin'
        }
    }
}
